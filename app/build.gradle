def final VERSION_CODE = 8
def final VERSION_NAME = '1.0.0-beta3'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.glodanif.bluetoothchat"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode VERSION_CODE
        versionName VERSION_NAME
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    signingConfigs {

        release {

            if (project.hasProperty("bluetoothchat.properties") &&
                    new File(project.property("bluetoothchat.properties")).exists()) {

                Properties properties = new Properties()
                properties.load(new FileInputStream(file(project.property("bluetoothchat.properties"))))

                storeFile file(properties['keystore'])
                storePassword properties['keystore.password']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            } else {
                null
            }
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.dev'
            versionNameSuffix '-DEV'
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
    }
}

dependencies {

    def final SUPPORT_VERSION = '27.0.2'
    def final ARCH_VERSION = '1.0.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_VERSION"

    compile "android.arch.persistence.room:runtime:$ARCH_VERSION"
    kapt "android.arch.persistence.room:compiler:$ARCH_VERSION"

    compile 'commons-io:commons-io:2.5'

    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.github.jkwiecien:EasyImage:2.0.4'
    compile 'com.squareup.picasso:picasso:2.6.0-SNAPSHOT'
    compile 'me.priyesh:chroma:1.0.2'
    compile 'com.github.chrisbanes:PhotoView:2.1.3'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$KOTLIN_VERSION"

    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$KOTLIN_VERSION"
    testCompile 'org.mockito:mockito-core:2.8.9'
    testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    testCompile 'junit:junit:4.12'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

task assembleReleaseAndOpen << {
    Runtime.getRuntime().exec("explorer.exe /select," +
            buildDir.getAbsolutePath() + "\\outputs\\apk\\release\\app-release.apk")
}

assembleReleaseAndOpen.dependsOn 'assembleRelease'
