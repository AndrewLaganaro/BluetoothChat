def final VERSION_CODE = 18
def final VERSION_NAME = '1.0.5'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.glodanif.bluetoothchat"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode VERSION_CODE
        versionName VERSION_NAME
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    signingConfigs {

        release {

            if (project.hasProperty("bluetoothchat.properties") &&
                    new File(project.property("bluetoothchat.properties")).exists()) {

                Properties properties = new Properties()
                properties.load(new FileInputStream(file(project.property("bluetoothchat.properties"))))

                storeFile file(properties['keystore'])
                storePassword properties['keystore.password']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            } else {
                null
            }
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.dev'
            versionNameSuffix '-DEV'
            buildConfigField "boolean", "AUTORESPONDER", "false"
        }

        autoresponder {
            initWith(debug)
            buildConfigField "boolean", "AUTORESPONDER", "true"
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "AUTORESPONDER", "false"
        }
    }

    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
    }
}

dependencies {

    def final SUPPORT_VERSION = '27.1.1'
    def final ARCH_VERSION = '1.0.0'
    def final DAGGER_VERSION = '2.4'
    def final ESPRESSO_VERSION = '3.0.0'
    def final COROUTINES_VERSION = '0.22.5'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    implementation "com.android.support:support-v4:$SUPPORT_VERSION"
    implementation "com.android.support:design:$SUPPORT_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_VERSION"

    implementation "android.arch.persistence.room:runtime:$ARCH_VERSION"
    kapt "android.arch.persistence.room:compiler:$ARCH_VERSION"

    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'com.github.jkwiecien:EasyImage:2.0.4'
    implementation 'com.squareup.picasso:picasso:2.6.0-SNAPSHOT'
    implementation 'me.priyesh:chroma:1.0.2'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$COROUTINES_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$COROUTINES_VERSION"

    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$KOTLIN_VERSION"
    testImplementation 'io.mockk:mockk:1.7.10'
    testImplementation 'junit:junit:4.12'

    androidTestImplementation "com.android.support:support-annotations:$SUPPORT_VERSION"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}

task assembleReleaseAndOpen << {
    Runtime.getRuntime().exec("explorer.exe /select," +
            buildDir.getAbsolutePath() + "\\outputs\\apk\\release\\app-release.apk")
}

assembleReleaseAndOpen.dependsOn 'assembleRelease'
